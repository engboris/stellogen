# Learn Stellogen

Stellogen is an experimental, logic-agnostic programming language built on
**term unification**. Instead of types or fixed logic, programs and meaning are
expressed with the same raw material.

This guide walks you through the basics.

---

## Let's start!

Follow the install instructions in the README file of the repository.

Open your favorite text editor and create a file `test.sg`. You can run your
file with `sgen run test.sg`.

Check that everything works fine by writing and running the following program:

```
(show "Hello, world")
```

It displays `(%string Hello, world)`. Do not worry about the shape of the
output: in Stellogen even strings are terms, and `show` displays the term
hidden behind the double quotes notation.

## Show

The `show` command is used to display an expression on the screen. It will be
useful through this whole guide.

---

## Comments

You can add comments to explain what your program does or to make it more
readable.

```stellogen
' single line

'''
multi
line
'''
```

## Everything is a term!

In stellogen **everything** is a term.

A **term** is either:

* A variable: starts with uppercase (`X`, `Y`, `Var`).
* A function: a sequence of symbols surrounded by parentheses, beginning with a
  lowercase or special symbol, followed by terms:

```
' this program does nothing but you can check the validity of expressions
(f a X)
(:: x t)

' several terms can be on the same line
(add 2 2) (fact 3)
```

In the special case of constants (function without argument) we can omit
parentheses:

```
a
$
a $ :b c
```

## Rays and compatibility

**Unification** = finding substitutions that make two terms identical.

You can check if two terms are matchable with the term `(~= t u)` where `t` and
`u` are terms.

For example:

```stellogen
(~= (f X)  (f (h a)))    ' they match with {X := (h a)}
' (~= (f X)  X)          ' ❌ fails with an error (circular)
' (~= (f X)  (g X))      ' ❌ fails with an error (different head symbol)
```

Note that `~=` checks *structural* unifiability and ignores polarity:
`(~= (+f X) (+f a))` succeeds even though two positive rays cannot interact.

A **ray** is a term with polarity:

* `(+f X)` → positive
* `(-f X)` → negative
* `(f X)`  → neutral (does not interact)

Two rays are **compatible** and can interact if they have opposite polarities
**and** their terms unify.

```stellogen
'''
(+f X) and (-f (h a))  are compatible with {X := (h a)}
(+f X) and (+f a)      are incompatible because they have same head polarity
'''
```

---

## Definitions and calls

Our most useful term will be definitions written `(def x t)` where `x` is the
name associated to the definition and `t` is a term. For example:

```stellogen
(def a (+f X))
```

You can invoke a definition by prefixing a name with `#`:

```stellogen
(show #a)
```

---

## Syntactic sugar

There are shorthands to build complex but useful terms.

### Cons lists

Square brackets inside a term build a list:

```stellogen
(show (list [a b c]))  ' [a b c] means (%cons a (%cons b (%cons c %nil))), a list containing a, b and c
(show (list []))       ' [] means %nil, the empty list
```

Beware: brackets are resolved by position. Inside a term they build a list,
but at the top level of a constellation they build a star (see below).

### Groups

```stellogen
(show { a b c })  ' means (%group a b c)
(show {})         ' means (%group), the empty group
```

---

## Make terms interact with Stars and Constellations

Stars and constellations are special terms which can interact with each other.

* A **star** is a block of rays:

```stellogen
[(+f X) (-f (h a)) (+g Y)]
```

Square brackets can be omitted when there is a single ray.

* A **constellation** is a group of stars:

```stellogen
{ (+f X) [(-f X) (+g a)] }
```

Variables are local to each star. So, in the above example, the `X` in `(+f X)`
and the one in `[(-f X) (+g a)]` are two distinct variables.

## Principles of Star Fusion

The idea of **fusion** is that stars can collide along compatible rays and merge.

When rays of two stars unify:

* They disappear (consumed).
* Their substitution applies to their neighbors rays.
* Their stars merge.

Example of interaction:

```stellogen
'''
star 1: [(+f X) X]
star 2: (-f a)

' connexion
(-f a) ------ (+f X) X

' annihilation and merge with resolution {X:=a}
X

' propagation
a         ' <-- this is the result of execution
'''
```

Note: this corresponds to the so-called Robinson's resolution rule in formal
logic.

## Focus and Action/State

For execution to even work in the first place, we need to group stars into
*actions* and *states*.

State stars are marked with `@`. They are the “targets” for interaction.
The other stars are actions.

For example:

```stellogen
' state:  @[-c d]
' action: [+a b]
(def d { [+a b] @[-c d] })
```

**Intuition:** Focus corresponds to distinguishing **data** from
**rules/program**:

* **States** (`@`) = what you're computing (the data being transformed)
* **Actions** (no `@`) = how you compute (the rules/program that transforms)

You can also focus all stars of a constellation with `@`:

```stellogen
(def f @{ [a] [b] [c] })
```

## Execution of Constellations

Execution = stars interacting through **fusion**.

Execution duplicates actions and fuses them with state stars until no more
interactions are possible. The result is a new constellation.

**Let's execute constellations!**

```stellogen
(def x [(+f X) X])
(def y (-f a))

(def res1 (exec @#x #y))  ' normal execution
(show #res1)

(def res2 (fire @#x #y))  ' actions are used exactly once
(show #res2)
```

You can watch fusion happen step by step with `sgen trace test.sg`. It is a
precious tool to understand what happens during an execution.

---

## Inequality Constraints

Add constraints with `[ some star || (!= X1 Y1) ... (!= Xn Yn)]`:

```stellogen
(def ineq {
  [(+f a)]
  [(+f b)]
  @[(-f X) (-f Y) (r X Y) || (!= X Y)]})
(show (exec #ineq))
```

where several equality constraints can be chained after `||`.

This prevents `X` and `Y` from unifying to the same concrete value.

---

## Relational Programming

Constellations can act like relational databases: facts are positive rays and
queries are focused stars with negative rays.

### Facts

```stellogen
(def edges {
  [(+edge a b)]
  [(+edge b c)]
})
```

### Query

A query asks for all values matching a pattern:

```stellogen
(show (exec #edges @[(-edge a X) (res X)]))
' => (res b)
```

### Rules

A rule is a single star with a positive conclusion and negative premises:

```stellogen
(def hop [(-edge X Y) (-edge Y Z) (+hop X Z)])
```

It reads: if there is an edge from `X` to `Y` and an edge from `Y` to `Z`,
then there is a hop from `X` to `Z`. Beware: since variables are local to
each star, a rule must be one star, otherwise its premises cannot share
variables with its conclusion.

### Putting it together

```stellogen
(def edges {
  [(+edge a b)]
  [(+edge b c)]
})

(def hop [(-edge X Y) (-edge Y Z) (+hop X Z)])

(show (exec { #edges #hop } @[(-hop a Z) (res Z)]))
' => (res c)
```

This asks: *where can we go from `a` in exactly two steps?*

Note that execution computes all answers at once by saturation. There is no
clause order, no search and no backtracking.

---

## Expect assertion

This is a more strict version of the matching `~=` term which expects
syntactic equality.

```stellogen
(== a a)    ' does nothing
' (== a b)  ' fails with an error
```

---

## Advanced syntax manipulation

### Parametric definitions

Definitions are actually terms like others so they can be *parametric*:

```stellogen
(def (initial Q) [(-i W) (+a W Q)])
```

In this case, calling

```stellogen
#(initial q0)
```

will replace `Q` by `q0` in `[(-i W) (+a W Q)]` so we get

```stellogen
[(-i W) (+a W q0)]
```

### Macros

**Macros** work like definitions except that are applied before actual
execution in a preprocessing phase:

```stellogen
' replace (assert X Y) by (== X Y) everywhere in the code
(macro (assert X Y) (== X Y))
(assert a a)
```

Notice that they do not involve any call with `#`, they replace terms.

You can display your code after macro expansion with `sgen preprocess test.sg`
to check what your macros actually do.

### Nested phantom constellations

Some terms like `def`, `show` or `==` are not interactive terms which can
be executed as constellations but they have an effect on the environment.

In fact, they can occur anywhere in the code and produce an effect when
evaluated but they are considered like empty constellations `{}`.

For example:

```stellogen
(exec [(+f X) X] (-f a) (def x hello1))
(show #x)
(def y (show hello2))
#y
```

---

## File import

It is possible to import the content of a file through their relative path:

```stellogen
(use "filename")
```

This imports both the definitions and the macros of the file. Paths are
resolved relative to the importing file.

---

## Types as Sets of Tests

In Stellogen, **types are sets of tests** that a constellation must pass to be
of that type.

For example, we define a type for natural numbers which is simply a
constellation corresponding to a "test". We use `spec` instead of `def`: it
works exactly the same but marks the intent that the definition is a
specification:

```stellogen
(spec nat {
  [(-nat 0) ok]
  [(-nat (s N)) (+nat N)]})
```

A constellation must pass **all tests** to be considered of type `nat`.

We then define the behavior of type assertions with a macro:

```stellogen
(macro (:: Tested Test) (== @(exec @#Tested #Test) ok))
```

It says that a `Tested` is of type `Test` when their interaction with focus on
`Tested` is equal to `ok`.

```stellogen
' passes the test
(def two (+nat (s (s 0))))
(:: two nat)

' does not pass the test
(def bad (+nat foo))
' (:: bad nat)
```

Notice that a constellation can have several types providing it passes all
the tests of those types.
