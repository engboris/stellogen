(use-macros "../milkyway/prelude.sg")

''' ======================================================== '''
''' FIRST-ORDER MULTIPLICATIVE LINEAR LOGIC PROOF-STRUCTURES '''
''' ======================================================== '''
' FOMLL proof-structures are MLL proof-structures
' (cf. examples/proofnets/mll.sg) which contain only cut and axiom
' constructors.
'
' An axiom between vertices x and y becomes the binary postive star
[+x +y]
'
' A cut between vertices y and z becomes the binary negative star
' which will link two axioms
[-y -z]

''' ======================================================== '''
''' FOR EXAMPLE '''
''' ======================================================== '''
' MLL proof-structures with only cuts and axioms yield partitions
' corresponding to axiom links with links between natural numbers.
'
'   ax    ax    ax
'   __    __    __
'  /  \  /  \  /  \
'  1  2  3  4  5  6
'  \   \____/__/
'   \   cut/
'    \____/
'     cut
'
'     becomes
'
'   [1, 2]  [3, 4]  [5, 6]
'    \   \______/____/
'     \________/
'
'    corresponding to the constellation
(:= x {
  [+1 +2] [+3 +4] [+5 +6]
      [-1 -4]  [-2 -5]
})
'     which evaluates into
'
'   [1, 6]  [3, 4]
'    \          /
'     \________/
'
'    then
'
'   [3, 6]
'
' This corresponds to the following execution in stellogen
' in which [-3 +3] is a way to initiate a starting point in computation
(:= comp (exec #x @[-3 +3]))
(:= res { [+3 +6] })
(== #comp #res)


''' ======================================================== '''
''' SUCCESSFUL TYPING '''
''' ======================================================== '''
' Typing works exactly the same as in examples/proofnets/mll.sg
' with the same specifications (providing you have the right adapters)
' except that the proof of identity (A ⊗ B) -o (A ⊗ B) is
(:= proof? { [+1 +3] [+2 +4]})
