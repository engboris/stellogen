(use-macros "../milkyway/prelude.sg")

(spec binary {
  [(-i []) ok]
  [(-i [0|X]) (+i X)]
  [(-i [1|X]) (+i X)]})

'input words
(def e (+i []))        (:: e binary)
(def 0 (+i [0]))       (:: 0 binary)
(def 000 (+i [0 0 0])) (:: 000 binary)
(def 010 (+i [0 1 0])) (:: 010 binary)
(def 110 (+i [1 1 0])) (:: 110 binary)

(def (initial Q) [(-i W) (+a W Q)])
(def (accept Q) [(-a [] Q) accept])
(def (if read C1 on Q1 then Q2) [(-a [C1|W] Q1) (+a W Q2)])

'''
automaton accepting words ending with 00
'''
(def a1 {
  #(initial q0)
  #(accept q2)
  #(if read 0 on q0 then q0)
  #(if read 0 on q0 then q1)
  #(if read 1 on q0 then q0)
  #(if read 0 on q1 then q2)})

(def kill (-a _ _))

(show (process (exec @#e #a1)   #kill))
(show (process (exec @#000 #a1) #kill))
(show (process (exec @#010 #a1) #kill))
(show (process (exec @#110 #a1) #kill))
