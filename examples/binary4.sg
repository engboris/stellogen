(use-macros "milkyway/prelude.sg")
(macro (:: Tested Test)
  (== (process #Test #Tested) ok))

(spec u4 [(-b _ 1 _) (-b _ 2 _) (-b _ 3 _) (-b _ 4 _) ok])

(def (make_bin Name X1 X2 X3 X4)
  { [(+b Name 1 X1)] [(+b Name 2 X2)] [(+b Name 3 X3)] [(+b Name 4 X4)] })

(def b1 #(make_bin b1 0 0 0 1)) (:: b1 u4)
(def b2 #(make_bin b2 0 0 1 1)) (:: b2 u4)

(show #b1)
(show #b2)

(def (if A = X then R = Z) [(-b A I X) (+b R I Z)])
(def (if A = X and B = Y then R = Z) [(-b A I X) (-b B I Y) (+b R I Z)])

(def (and A B R) {
  #(if A = 0 and B = _ then R = 0)
  #(if A = 1 and B = X then R = X) })
(def rand (process #b1 #(and b1 b2 r) #b2))
(show #rand)
(== #rand #(make_bin r 0 0 0 1))

(def (or A B R) {
  #(if A = 0 and B = X then R = X)
  #(if A = 1 and B = Y then R = 1) })
(def ror (process #b1 #(or b1 b2 r) #b2))
(show #ror)
(== #ror #(make_bin r 0 0 1 1))

(def (not A R) {
  #(if A = 1 then R = 0)
  #(if A = 0 then R = 1) })
(def rnot (process #b1 #(not b1 r)))
(show #rnot)
(== #rnot #(make_bin r 1 1 1 0))

(def rnand (process #b1 #(and b1 b2 r1) #b2 #(not r1 r2)))
(show #rnand)

'''
(def rxor (process #rnand #(and r2 r r3) #ror))
(show #rxor)
'''
