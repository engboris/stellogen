(spec binary [
  [(-i e) ok]
  [(-i [0 X]) (+i X)]
  [(-i [1 X]) (+i X)]])

'input words
(:= e (+i e))
(:: e binary)

(:= 000 (+i [0 0 0 e]))
(:: 000 binary)

(:= 010 (+i [0 1 0 e]))
(:: 010 binary)

(:= 110 (+i [1 1 0 e]))
(:: 110 binary)

'''
automaton accepting words ending with 00
'''
(:= a1 [
  [(-i W) (+a W q0)]
  [(-a e q2) accept]
  [(-a [0 W] q0) (+a W q0)]
  [(-a [0 W] q0) (+a W q1)]
  [(-a [1 W] q0) (+a W q0)]
  [(-a [0 W] q1) (+a W q2)]])

<show kill exec (union @#e #a1)>
<show kill exec (union @#000 #a1)>
<show kill exec (union @#010 #a1)>
<show kill exec (union @#110 #a1)>
