(use-macros "milkyway/prelude.sg")

(spec binary {
  [(-i []) ok]
  [(-i [0|X]) (+i X)]
  [(-i [1|X]) (+i X)]})

'input words
(:= e (+i []))        (:: e binary)
(:= 0 (+i [0]))       (:: 0 binary)
(:= 000 (+i [0 0 0])) (:: 000 binary)
(:= 010 (+i [0 1 0])) (:: 010 binary)
(:= 110 (+i [1 1 0])) (:: 110 binary)

(:= (initial Q) [(-i W) (+a W Q)])
(:= (accept Q) [(-a [] Q) accept])
(:= (if read C1 on Q1 then Q2) [(-a [C1|W] Q1) (+a W Q2)])

'''
automaton accepting words ending with 00
'''
(:= a1 {
  #(initial q0)
  #(accept q2)
  #(if read 0 on q0 then q0)
  #(if read 0 on q0 then q1)
  #(if read 1 on q0 then q0)
  #(if read 0 on q1 then q2)})

(:= kill (-a _ _))

(show (process (exec @#e #a1)   #kill))
(show (process (exec @#000 #a1) #kill))
(show (process (exec @#010 #a1) #kill))
(show (process (exec @#110 #a1) #kill))
